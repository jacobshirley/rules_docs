"""Declare runtime dependencies

These are needed for local dev, and users must install them as well.
See https://docs.bazel.build/versions/main/skylark/deploying.html#dependencies
"""

_DOC = "Fetch external tools needed for docgen toolchain"
_ATTRS = {
    "pypi_hub": attr.label(
        doc = "Name of the pip repository hub that contains mkdocs",
        mandatory = True,
    ),
    "plugins": attr.string_list(
        doc = "List of mkdocs plugins to install",
        mandatory = False,
        default = [],
    ),
}

def _mkdocs_repository_impl(repository_ctx):
    pypi_hub = repository_ctx.attr.pypi_hub.repo_name

    plugin_content = ""
    for plugin in repository_ctx.attr.plugins:
        plugin_content += '\n        "@@{pypi_hub}//'.format(
            pypi_hub = pypi_hub,
        ) + plugin.replace("-", "_") + '",'

    build_content = """# Generated by docgen/repositories.bzl
load("@rules_python//python/entry_points:py_console_script_binary.bzl", "py_console_script_binary")

py_console_script_binary(
    name = "mkdocs",
    pkg = "@@{pypi_hub}//mkdocs",
    script = "mkdocs",
    visibility = ["//visibility:public"],
    deps = [{plugins}]
)
""".format(
        pypi_hub = pypi_hub,
        plugins = plugin_content,
    )

    # Base BUILD file for this repository
    repository_ctx.file("BUILD.bazel", build_content)

    defs_content = """# Generated by docgen/repositories.bzl
load("@jacobshirley_rules_docgen//docgen/private:mkdocs_build.bzl", _mkdocs_build = "mkdocs_build")
load("@jacobshirley_rules_docgen//docgen/private:mkdocs_serve.bzl", _mkdocs_serve = "mkdocs_serve")
load("@jacobshirley_rules_docgen//docgen/private:mkdocs_config.bzl", _mkdocs_config = "mkdocs_config")

mkdocs_config = _mkdocs_config

def mkdocs_build(**kwargs):
    return _mkdocs_build(mkdocs_executable = "@@{repo_name}//:mkdocs", **kwargs)

def mkdocs_serve(**kwargs):
    return _mkdocs_serve(mkdocs_executable = "@@{repo_name}//:mkdocs", **kwargs)

""".format(
        repo_name = repository_ctx.name,
    )
    repository_ctx.file("defs.bzl", defs_content)

mkdocs_repository = repository_rule(
    _mkdocs_repository_impl,
    doc = _DOC,
    attrs = _ATTRS,
)
